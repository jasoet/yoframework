plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm"
    id "com.google.devtools.ksp"
    id "dev.detekt"
    id "idea"
    id "maven-publish"
    id "jacoco"
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
    }
}

group = 'com.github.jasoet'

dependencies {

    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"

    api "io.arrow-kt:arrow-core:$arrowVersion"
    api "io.arrow-kt:arrow-typeclasses:$arrowVersion"

    api "io.arrow-kt:arrow-instances-data:$arrowVersion"
    api "io.arrow-kt:arrow-instances-core:$arrowVersion"

    api "io.arrow-kt:arrow-data:$arrowVersion"
    api "io.arrow-kt:arrow-syntax:$arrowVersion"
    ksp "io.arrow-kt:arrow-annotations-processor:$arrowVersion"

    api "com.google.dagger:dagger:$daggerVersion"
    ksp "com.google.dagger:dagger-compiler:$daggerVersion"
    kspTest "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.amshove.kluent:kluent:$kluentVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"

    testImplementation("org.jetbrains.spek:spek-api:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }

//    detektPlugins "dev.detekt:detekt-formatting:$detektVersion"
}

kotlin {
    jvmToolchain(25)
}

tasks {

    compileKotlin {
        kotlinOptions {
            apiVersion = "2.3"
            languageVersion = "2.3"
            allWarningsAsErrors = false
        }
    }

    compileTestKotlin {
        kotlinOptions {
            apiVersion = "2.3"
            languageVersion = "2.3"
            allWarningsAsErrors = false
//            freeCompilerArgs = [
////                "-Xuse-k2", "-Xbackend-threads=2"
//            ]
        }
    }

    test {
        finalizedBy jacocoTestReport

        useJUnitPlatform {
            includeEngines 'spek', 'junit-jupiter'
        }

        testLogging {
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = ["passed", "skipped", "failed", "standardError"]
        }
    }

    jacoco {
        toolVersion = "0.8.13"
    }

    jacocoTestReport {
        dependsOn test // tests are enabled to run before generating the report
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = file("${getLayout().getBuildDirectory()}/jacocoHtml")
        }
    }
}

tasks.register('compile') { dependsOn "testClasses" }

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

detekt {
    // Version of detekt that will be used. When unspecified the latest detekt
    // version found will be used. Override to stay on the same version.
    toolVersion = "2.0.0-alpha.1"

    // The directories where detekt looks for source files.
    // Defaults to `files("src/main/java", "src/test/java", "src/main/kotlin", "src/test/kotlin")`.
    source = files(
            "src/main/kotlin",
            "gensrc/main/kotlin"
    )

    // Builds the AST in parallel. Rules are always executed in parallel.
    // Can lead to speedups in larger projects. `false` by default.
    parallel = true

    // Define the detekt configuration(s) you want to use.
    // Defaults to the default detekt configuration.
    config.setFrom("$rootDir/detekt.yml")

    // Applies the config files on top of detekt's default config file. `false` by default.
    buildUponDefaultConfig = true

    // Turns on all the rules. `false` by default.
    allRules = false

    // Specifying a baseline file. All findings stored in this file in subsequent runs of detekt.
//    baseline = file("path/to/baseline.xml")

    // Disables all default detekt rulesets and will only run detekt with custom rules
    // defined in plugins passed in with `detektPlugins` configuration. `false` by default.
    disableDefaultRuleSets = false

    // Adds debug output during task execution. `false` by default.
    debug = false

    // If set to `true` the build does not fail when there are any issues.
    // Defaults to `false`.
    ignoreFailures = true

    // The build fails when there is at least one issue with this severity (or above).
    // If set to `Never`, the task will not fail regardless of the number of issues and their severities.
    // If `ignoreFailures` is set to `true`, the value of this property is ignored.
    // Defaults to `Error`
//    failOnSeverity = dev.detekt.gradle.extensions.FailOnSeverity.Error

    // Android: Don't create tasks for the specified build types (e.g. "release")
    ignoredBuildTypes = ["release"]

    // Android: Don't create tasks for the specified build flavor (e.g. "production")
    ignoredFlavors = ["production"]

    // Android: Don't create tasks for the specified build variants (e.g. "productionRelease")
    ignoredVariants = ["productionRelease"]

    // Specify the base path for file paths in the formatted reports.
    // If not set, all file paths reported will be absolute file path.
    basePath = projectDir
}
tasks.detekt.jvmTarget = "24"

def pomConfig = {
    name = "Yo Framework"
    description = "Thin Kotlin DSL on top of Dagger and Vert.x"
    url = "https://yoframework.id"
    licenses {
        license {
            name = "The Apache Software License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution = "repo"
        }
    }
    developers {
        developer {
            id = "jasoet"
            name = "Deny Prasetyo"
            email = "jasoet87@gmail.com"
        }
    }
    scm {
        url = "https://github.com/jasoet/yoframework"
    }
}

publishing {
    publications {
        YoPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'com.github.jasoet.yoframework'
            artifactId "${project.name}"
            version "${version}"
            pom pomConfig
        }
    }

//    repositories {
//        maven {
//            name = "GitHubPackages"
//            url = "https://maven.pkg.github.com/jasoet/yoframework"
//            credentials {
//                username = System.getenv("GITHUB_ACTOR")
//                password = System.getenv("GITHUB_TOKEN")
//            }
//        }
//    }
}
